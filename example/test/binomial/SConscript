# -*-python-*-
from distutils import sysconfig
import os


Import( "env" )

lib_env = env.Clone()
lib_env.Append(CPPPATH = ['binomial'])
#lib_env.AppendUnique(CXXFLAGS=["-Wl,-rpath=."])
lib_env.AppendUnique(CXXFLAGS=["-Wwrite-strings"])
includes= ['binomial.h']
sources = ['binomial.cpp']

target = 'binomial'
if lib_env.get("static"):
    lib = lib_env.StaticLibrary(target, sources)
else:
    lib = lib_env.SharedLibrary(target, sources)
Alias("build", lib)

wrap_env = lib_env.Clone()
sources = wrap_env.Glob('wrapper_*.cpp')
sources += ['module.cpp']
if len(sources) > 0:
    wrap = wrap_env.LoadableModule('_module', sources,
                                   CPPPATH = [sysconfig.get_python_inc()] + wrap_env['CPPPATH'],
                                   CXXFLAGS= wrap_env['CXXFLAGS'],
                                   CPPDEFINES = ['BOOST_PYTHON_DYNAMIC_LIB'],
                                   LIBPATH = [sysconfig.get_config_var('LIBDIR'), os.path.abspath('.')],
                                   LIBS = [target, 'boost_python', 'python' + sysconfig.get_config_var('VERSION')],
                                   LIBPREFIX = '')
    Alias("build", wrap)

